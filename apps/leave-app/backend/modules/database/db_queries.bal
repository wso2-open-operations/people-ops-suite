// Copyright (c) 2025 WSO2 LLC. (https://www.wso2.com).
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.
import ballerina/sql;

# Generates a common SQL query to fetch leave records from the database.
#
# + return - Select query
isolated function getCommonLeaveQuery() returns sql:ParameterizedQuery => `
    SELECT 
        id, 
        email, 
        leave_type,
        leave_period_type,
        copy_email_list,
        notify_everyone,
        submit_note,
        cancel_note,
        created_date,
        updated_date,
        email_id,
        email_subject,
        start_date,
        end_date,
        start_half,
        end_half,
        canceled_date,
        num_days,
        public_submit_note,
        calendar_event_id,
        location,
        status,
        approver_email
    FROM leave_app.leave_submissions
`;

# Query to fetch leaves from the database.
#
# + filter - Query filter  
# + 'limit - Maximum records limit  
# + offset - Offset value
# + return - Select query fetch leaves
isolated function getLeavesQuery(LeaveFilter filter, int? 'limit = (), int? offset = ())
    returns sql:ParameterizedQuery {

    sql:ParameterizedQuery mainQuery = getCommonLeaveQuery();

    sql:ParameterizedQuery[] filterQueries = [];

    string[]? emails = filter?.emails;
    if emails is string[] && emails.length() > 0 {
        filterQueries.push(sql:queryConcat(`email IN (`, sql:arrayFlattenQuery(emails), `)`));
    }
    string? startDate = filter?.startDate;
    string? endDate = filter?.endDate;
    if startDate is string && endDate is string {
        filterQueries.push(`((start_date BETWEEN ${startDate} AND ${endDate}) 
            OR (end_date BETWEEN ${startDate} AND ${endDate}) 
            OR (start_date < ${startDate} AND end_date > ${endDate}))`);
    } else if startDate is string {
        filterQueries.push(
            `(start_date >= ${startDate} OR (end_date >= ${startDate} AND start_date < ${startDate}))`
        );
    } else if endDate is string {
        filterQueries.push(`(end_date <= ${endDate} OR (start_date <= ${endDate} AND end_date > ${endDate}))`);
    }

    if filter?.approverEmail is string {
        filterQueries.push(sql:queryConcat(` approver_email = ${filter?.approverEmail} `));
    }
    Status[]? statuses = filter?.statuses;
    if statuses is Status[] && statuses.length() > 0 {
        filterQueries.push(sql:queryConcat(` status IN (`, sql:arrayFlattenQuery(statuses), `) `));
    }
    
    string[]? leaveTypes = filter?.leaveTypes;
    if leaveTypes is string[] && leaveTypes.length() != 0 {
        filterQueries.push(sql:queryConcat(`leave_type IN (`, sql:arrayFlattenQuery(leaveTypes), `)`));
    }

    sql:ParameterizedQuery sqlQuery = buildSqlQuery(mainQuery, filterQueries);
    sql:ParameterizedQuery orderBy = filter?.orderBy == ASC ? `ASC` : `DESC`;
    return sql:queryConcat(sqlQuery, ` ORDER BY created_date `, orderBy,
            ` LIMIT ${'limit ?: MAXIMUM_LIMIT_VALUE} OFFSET ${offset ?: 0}`);
}

# Query to insert leave to the database.
#
# + input - Leave input data  
# + numberOfDays - Number of days for the leave  
# + location - Employee location
# + return - Insert Query
isolated function insertLeaveQuery(LeaveInput input, float numberOfDays, string location)
    returns sql:ParameterizedQuery {

    boolean? isMorningLeave = input.isMorningLeave;
    sql:ParameterizedQuery insertQuery = `
        INSERT INTO leave_app.leave_submissions (
            email,
            leave_type,
            leave_period_type,
            copy_email_list,
            notify_everyone,
            submit_note,
            cancel_note,
            created_date,
            email_id,
            email_subject,
            status,
            approver_email,
            start_date,
            end_date,
            start_half,
            num_days,
            public_submit_note,
            calendar_event_id,
            location,
            active
        ) VALUES (
            ${input.email},
            ${input.leaveType},
            ${input.periodType},
            ${string:'join(",", ...input.emailRecipients)},
            0,
            ${input?.comment ?: ""},
            '',
            NOW(),
            ${input.emailId ?: ""},
            ${input.emailSubject},
            ${input.status},
            ${input.approverEmail},
            TIMESTAMP(${input.startDate}, ${isMorningLeave is boolean ? (isMorningLeave ? "00:00:00" : "13:00:00") : "00:00:00"}),
            TIMESTAMP(${input.endDate}, ${isMorningLeave is boolean ? (isMorningLeave ? "13:00:00" : "23:59:00") : "23:59:00"}),
            ${isMorningLeave is boolean ? !isMorningLeave : ()},
            ${numberOfDays},
            ${input?.isPublicComment ?: false},
            ${input?.calendarEventId},
            ${location},
            '1')
    `;

    return insertQuery;
}

# Query to get sabbatical leave approval status records (for leads).
#
# + leadEmail - Email of the lead
# + status - List of approval statuses to filter (APPROVED, REJECTED, PENDING)
# + return - Select query to get sabbatical leave approval status
isolated function getLeaveApprovalStatusListQuery(string leadEmail, string[] status)
    returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = sql:queryConcat(`
        SELECT 
            id as id,
            status as approvalStatus,
            email as email,
            start_date as startDate,
            end_date as endDate,
            submit_note as submitNote
        FROM leave_submissions WHERE approver_email = ${leadEmail} AND status IN (`,
            sql:arrayFlattenQuery(status), `)`);

    return query;
}

# Query to get Leave Approver Email by Leave ID.
#
# + leaveId - Leave ID
# + return - Query to get leave approver email
isolated function getLeaveApproverEmailByIdQuery(int leaveId) returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        SELECT 
            approver_email
        FROM leave_submissions
        WHERE id = ${leaveId}
    `;
    return query;
}

# Query to approve or reject a  leave application (for leads).
#
# + approvalStatus - Status of the leave to be set (APPROVED, REJECTED)
# + leaveId - ID of the leave record 
# + return - Update query to approve or reject leave application
isolated function setLeaveApprovalStatusQuery(ApprovalStatus approvalStatus, int leaveId)
    returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        UPDATE
            leave_submissions
        SET
            status = ${approvalStatus}
        WHERE 
            id = ${leaveId}
    `;

    return query;
}

# Query to get leave submission info by leave ID.
#
# + leaveId - ID of the leave record
# + return - Query to get leave submission info
isolated function getLeaveSubmissionInfoByApprovalIdQuery(int leaveId)
    returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        SELECT 
            email,
            start_date as startDate,
            end_date as endDate
        FROM leave_submissions WHERE id = ${leaveId}
    `;

    return query;
}

# Query to get the end date of the last approved sabbatical leave for an employee.
#
# + employeeEmail - Email of the employee
# + return - Query to get the end date of the last sabbatical leave
isolated function getLastSabbaticalLeaveEndDateQuery(string employeeEmail) returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        SELECT 
            end_date
        FROM leave_submissions
        WHERE email = ${employeeEmail} AND leave_type = ${SABBATICAL_LEAVE} AND status = ${APPROVED}
        ORDER BY end_date DESC
        LIMIT 1
    `;
    return query;
}

# Query to get subordinates count on sabbatical leave.
#
# + leadEmail - Email of the lead
# + return - Select query to get count of subordinates on sabbatical leave
isolated function getSubordinateCountOnSabbaticalLeaveQuery(string leadEmail) returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        SELECT 
            COUNT(id)
        FROM 
            leave_submissions
        WHERE
            approver_email = ${leadEmail} AND status = ${APPROVED} AND leave_type = ${SABBATICAL_LEAVE} AND
            start_date <= UTC_TIMESTAMP() AND end_date >= UTC_TIMESTAMP()
    `;
    return query;
}

# Query to get most recent email notification recipient list (general leave) for a leave applicant.
#
# + applicantEmail - Email of the leave applicant
# + return - Select query to get email notification list
isolated function getEmailNotificationRecipientListQuery(string applicantEmail) returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        SELECT
            copy_email_list
        FROM
            leave_submissions
        WHERE 
            email = ${applicantEmail} AND leave_type <> ${SABBATICAL_LEAVE}
        ORDER BY
            created_date DESC
        LIMIT 1
    `;
    return query;
};

# Query to set the calendar event ID for a sabbatical leave record.
#
# + leaveId - Leave ID
# + calendarEventId - Calendar event ID
# + return - Update query to set calendar event ID
isolated function setSabbaticalLeaveCalendarEventIdQuery(int leaveId, string calendarEventId)
    returns sql:ParameterizedQuery {
    sql:ParameterizedQuery query = `
        UPDATE leave_submissions
        SET calendar_event_id = ${calendarEventId}
        WHERE id = ${leaveId}
    `;
    return query;
}
